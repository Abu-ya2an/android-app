apply plugin: 'com.android.application'
apply plugin: 'io.sentry.android.gradle'
apply plugin: 'kotlin-android'

def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(rootProject.file("keystore.properties")))

android {
    signingConfigs {
        release {
            keyAlias keystoreProperties['release.key.alias']
            keyPassword keystoreProperties['release.key.password']
            storeFile file(keystoreProperties['release.store.file'])
            storePassword keystoreProperties['release.store.password']
        }
    }

    compileSdkVersion 28
    defaultConfig {
        applicationId "net.ivpn.client"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 75
        versionName "1.65"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        testInstrumentationRunnerArguments clearPackageData: 'true'
        vectorDrawables.useSupportLibrary = true

        manifestPlaceholders = [ SENTRY_DSN:keystoreProperties['sentry.dsn']]
        buildConfigField "String", "BILLING_PUBLIC_KEY", keystoreProperties['billing.public.key']
    }

    flavorDimensions "spread", "api"
    productFlavors {
        store {
            dimension "spread"
            buildConfigField "String", "BUILD_VARIANT", "\"store\""
        }

        site {
            dimension "spread"
            buildConfigField "String", "BUILD_VARIANT", "\"site\""
        }

        production {
            dimension "api"
            buildConfigField "String", "BASE_URL", "\"api.ivpn.net\""
        }

        stage {
            dimension "api"
            buildConfigField "String", "BASE_URL", keystoreProperties['staging.url']
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            signingConfig signingConfigs.debug
        }
    }
    lintOptions {
        checkReleaseBuilds false
    }

    sourceSets {
        main {
            jniLibs.srcDir 'src/main/libs'
        }
    }

    dataBinding {
        enabled = true
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    testOptions {
        execution 'ANDROIDX_TEST_ORCHESTRATOR'
        unitTests.returnDefaultValues = true
    }
}
ext {
    supportLibsVersion = '28.0.0'
    streamsupportVersion = '1.7.1'
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // Retrofit
    implementation 'com.squareup.retrofit2:retrofit:2.5.0'

    // JSON Parsing
    implementation 'com.google.code.gson:gson:2.8.5'
    implementation 'com.squareup.retrofit2:converter-gson:2.5.0'

    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.annotation:annotation:1.1.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'com.google.android.material:material:1.1.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.legacy:legacy-preference-v14:1.0.0'

    //Used to support stream API from JAVA8 and JAVA9
    implementation "net.sourceforge.streamsupport:android-retrofuture:$streamsupportVersion"
    implementation "net.sourceforge.streamsupport:android-retrostreams:$streamsupportVersion"

//    Purchases
    implementation 'com.android.billingclient:billing:2.1.0'
    
    //For crash logging
    implementation 'io.sentry:sentry-android-core:2.0.+'

    //Powerful library for logging
    implementation 'org.slf4j:slf4j-api:1.7.24'
    implementation 'org.slf4j:jul-to-slf4j:1.7.0'
    implementation 'com.github.tony19:logback-android:2.0.0'

    implementation "junit:junit:$rootProject.ext.junitVersion"

    testImplementation "junit:junit:$rootProject.ext.junitVersion"
    testImplementation "org.mockito:mockito-core:$rootProject.ext.mockitoVersion"

    testImplementation 'org.powermock:powermock-api-mockito:1.6.2'
    testImplementation 'org.powermock:powermock-module-junit4-rule-agent:1.6.2'
    testImplementation 'org.powermock:powermock-module-junit4-rule:1.6.2'
    testImplementation 'org.powermock:powermock-module-junit4:1.6.2'

    implementation 'com.google.dagger:dagger:2.24'
    annotationProcessor 'com.google.dagger:dagger-compiler:2.24'

    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestUtil 'androidx.test:orchestrator:1.2.0'

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.2.1'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.2.1'
}

repositories {
    mavenCentral()
}